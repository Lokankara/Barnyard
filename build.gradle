plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '3.0.1'
}

group = 'com.yarn.barn'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

node {
    version = '19.6.1'
    npmVersion = '9.1.0'
    download = true
    nodeModulesDir = file('src/ui')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('npmInstall') {
    workingDir = file('src/ui')
}

tasks.register('npmRunBuild', NpmTask) {
    group = 'frontend'
    description = 'Build the frontend'
    dependsOn tasks.named('npmInstall')
    args = ['run', 'build']
    workingDir = file('src/ui')
}

tasks.register('copyBuildFiles', Copy) {
    from 'src/ui/build'
    into 'build/resources/main/static'
}

tasks.named('build') {
    dependsOn tasks.named('npmRunBuild')
    dependsOn tasks.named('copyBuildFiles')
}
